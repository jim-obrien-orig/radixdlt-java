buildscript {
    repositories {
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath 'org.eclipse.jgit:org.eclipse.jgit:5.0.1.201806211838-r'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.7'
        classpath 'com.adarshr:gradle-test-logger-plugin:1.6.0'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

group = 'com.radixdlt'
// Do NOT override project version from CLI, gradle.properties, etc.
if (version == 'unspecified') {
    // Add the -SNAPSHOT suffix so that we do not clash with artifacts published by jitpack
    version = org.eclipse.jgit.api.Git.open(project.projectDir).describe().call() + '-SNAPSHOT'
}

allprojects {
    version = rootProject.version

    configurations.all {
        resolutionStrategy {
            // fail eagerly on version conflict (includes transitive dependencies)
            // e.g. multiple different versions of the same dependency (group and name are equal)
            failOnVersionConflict()
        }
    }

    apply plugin: 'java'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        jcenter()
    }
}
